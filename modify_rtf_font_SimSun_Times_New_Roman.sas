/*************************************************************************************************
File name:      modify_rtf_font.sas
 
Study:          
 
SAS version:    9.4
 
Purpose:        Modify the fonts of an rtf document generated by SAS
 
Macros called:
 
Notes:
 
Parameters:
 
Sample:
 
Date started:    11APR2024
Date completed:
 
Mod     Date            Name            Description
---     -----------     ------------    -----------------------------------------------
1.0     11APR2024       Xinwei.Zhong    Created
 
 
************************************ Prepared by xxxx ************************************/

%macro m_rtf_SimSun_Times_New_Roman(rtfloc=
					,rtfname=
					,outpath=
					,outputname=
					,debug=);

%put NOTE: -------------------- Macro[&SYSMACRONAME.] Start --------------------;
%put @@-*-*-*-*-*-*-*-*-*-*-*-*-**-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*--*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*@@;
%put   Purpose: Modify the fonts of an rtf document generated by SAS | Author: xinwei.zhong 2024-04-11;
%put @@-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*@@;

%************************************************************************************;
%if %length(&rtfloc.)=0 %then %do;
	%put ERROR: Parameter[rtfloc] is uninitialized, please check!!; %return;
%end;
%let rtfloc=%sysfunc(tranwrd(&rtfloc.,\,/));
%if %sysfunc(fileexist(&rtfloc.))=0 %then %do; 
	%put ERROR: Path[rtfloc=&rtfloc.] no exist, please check!; 
%end;
%if %length(&outpath.)=0 %then %let outpath=%str(&rtfloc.);
%let outpath=%sysfunc(tranwrd(&outpath.,\,/));

%if %length(&rtfname.)>0 %then %do;
	%if %index(&rtfname.,%str(.))=0 %then %let rtfname=%str(&rtfname..rtf);
		%else %if "%upcase(%scan(&rtfname.,-1,.))"^="RTF" %then %do;
			 %put ERROR: File[&rtfloc./&rtfname.] no rtf file, please check!; %return;
		%end;
	%if %sysfunc(fileexist(&rtfloc./&rtfname.))=0 %then %do; 
		%put ERROR: File[&rtfloc./&rtfname.] no exist, please check!;  %return;
	%end;
	%if %length(&outputname.)=0 %then %let outputname=%scan(&rtfname.,1,%str(.))_modify.rtf;
	%if %index(&outputname.,%str(.))=0 %then %let outputname=%str(&outputname..rtf);
	%put &rtfname. | &outputname.;
%end; %else %do;
	%put ERROR: Parameter[rtfname] is uninitialized, please check!!; %return;
%end;
%if %length(&debug.)=0 %then %let debug=0;

data __rtff; 
	infile "&rtfloc./&rtfname." length=linelen lrecl=5000 recfm=v;
	input linein $varying5000. linelen;
	n=_n_;
run;
data __rtffont;
	set __rtff;
	where n<10 and prxmatch('#(\{\\f\d+\\)([^;\]]+)(;})#',linein);
run;
%let __fontls=;
proc sql noprint;
	select min(n),max(n) into: __minfontn,:__maxfontn from __rtffont;
quit;
*write file;
data __rtffont;
	set __rtffont %if &__minfontn.=&__maxfontn. %then %do; __rtffont %end; ;
	if _n_=1 then linein='{\f1\froman\fprq2\fcharset134\cpg936 SimSun;}';
	if _n_=2 then linein='{\f2\froman\fprq2\fcharset134\cpg936 Times New Roman;}';
run;
%if "&debug."="0" %then %do;
data _null_;
	set __rtff(where=(n<&__minfontn.)) __rtffont __rtff(where=(n>&__maxfontn.));
	file "&outpath.\&outputname." lrecl = 32767;						
	if n>20 then do;
		if prxmatch('#\\f1\\f2(\\| )#',linein)=0 then linein=prxchange("s/\\f(\d+)(\\| )/\\f1\\f2\\f$1$2/",-1,linein);
		linein=prxchange("s/(\\f1\\f2)(\\f1|\\f2)(\\| )/\\f1\\f2$3/",-1,linein);
	end;
	linelen =length(linein);
	put linein $varying5000. linelen;
run;

proc datasets nolist;
	delete __rtff: ;
quit;
%end;

%put NOTE: -------------------- Macro[&SYSMACRONAME.] End --------------------;

%mend m_rtf_SimSun_Times_New_Roman;

/*
dm "output;clear;log;clear;";
proc delete data=_all_; run;

%let rtfloc=%str(E:\Study-File\Macro test\Modify_rtf_font\testrtf);

%m_rtf_SimSun_Times_New_Roman(rtfloc=%str(&rtfloc.)
		,rtfname=%str(rtf-test.rtf)
		,outpath=%str(&rtfloc.)
		,outputname=%str(),debug=0);

*/

